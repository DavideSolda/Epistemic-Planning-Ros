entrypoints Program;
Prg.   Program   ::= [ Decl ] ;

Def.   Decl      ::= Ident  "of" "type" Ident ;
Exec.  Decl      ::= "executable" Action "if" [ Literal ] ;
Caus.  Decl      ::= Action "causes" [ Literal ] ;
CausCond.  Decl  ::= Action "causes" [ Literal ] "if" [ Literal ];
Init.  Decl      ::= "initially" Literal ;
Final. Decl      ::= "finally" Literal ;

Preds. Predicate ::= Ident "(" [ Exp ] ")" ;
Pred.  Predicate ::= Ident ;

LitN.  Literal   ::= "neg" Predicate ;
Lit.   Literal   ::= Predicate ;
LitE.  Literal   ::= Exp "==" Exp ;
LitNE. Literal   ::= Exp "!=" Exp ;

Acts.  Action    ::= Ident "(" [ Exp ] ")" ;
Act.   Action    ::= Ident ;

EAdd. 	Exp 	::= 	Exp 	"+" 	Exp1 	;
ESub. 	Exp 	::= 	Exp 	"-" 	Exp1 	;
EMul. 	Exp1 	::= 	Exp1 	"*" 	Exp2 	;
EDiv. 	Exp1 	::= 	Exp1 	"/" 	Exp2 	;
EMod. 	Exp1 	::= 	Exp1 	"\\" 	Exp2 	;
EInt. 	Exp2 	::= 	Integer 	;
EIdent. Exp2 	::= 	Ident 	;

coercions 	Exp 	2 	;

separator  nonempty Literal ",";
separator  nonempty Ident ",";
separator  nonempty Exp ",";
terminator nonempty Decl ".";
comment "%" ;
